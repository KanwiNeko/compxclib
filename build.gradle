plugins {
    // Apply the org.jetbrains.kotlin.jvm Plugin to add support for Kotlin.
    id 'org.jetbrains.kotlin.jvm' version '1.6.21'

    // Apply the java-library plugin for API and implementation separation.
    id 'java-library'

    // Maven to build to the local library
    id 'maven-publish'

    // shadow jar, used to build fat jars
    id 'com.github.johnrengelman.shadow' version '7.1.2'

    // singing for gpg keys
    id 'signing'
}

group = 'gay.kanwi'
version = 'v0.1-beta'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    // Use Maven Central for resolving dependencies.
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
    // Align versions of all Kotlin components
    implementation platform('org.jetbrains.kotlin:kotlin-bom')

    // Use the Kotlin JDK 8 standard library.
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'

    //kotlin reflection library
    implementation 'org.jetbrains.kotlin:kotlin-reflect:1.4.31'

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation 'com.google.guava:guava:31.0.1-jre'

    // Use the Kotlin test library.
    testImplementation 'org.jetbrains.kotlin:kotlin-test'

    // Use the Kotlin JUnit integration.
    testImplementation 'org.jetbrains.kotlin:kotlin-test-junit'

    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'org.apache.commons:commons-math3:3.6.1'
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'gay.kanwi'
            artifactId = 'compxclib'
            version = 'v0.1-beta'

            from components.java

            pom {
                name = 'CompxCLib'
                description = 'Complex Complete Library is a feature rich complex numbers library for Kotlin and Java '
                url = 'https://github.com/KatieUmbra/compxclib'
                inceptionYear = '2023'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/KatieUmbra/compxclib/blob/main/LICENSE'
                    }
                }

                developers {
                    developer {
                        id = 'Kanwi'
                        name = 'Katherine Chesterfield'
                        email = 'business@kanwi.gay'
                    }
                }
                scm {
                    connection = 'scm:git:git:github.com/KatieUmbra/compxclib.git'
                    developerConnection = 'scm:git:ssh://github.com/KatieUmbra/compxclib.git'
                    url = 'https://github.com/KatieUmbra/compxclib'
                }
            }
            repositories {
                maven {
                    name = "OSSRH"
                    url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                    credentials {
                        username = project.properties["ossrhUsername"]
                        password = project.properties["ossrhPassword"]
                    }
                }
            }
        }
    }
}

wrapper {
    gradleVersion = "7.3.3"
    distributionType = Wrapper.DistributionType.ALL
}

signing {
    sign publishing.publications.mavenJava
}